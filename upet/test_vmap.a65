

	; this test test the Micro-PET's capability of mapping
	; the 4k video memory into bank 8 VRAM address space.
	;
	; it's not testing the memory itself, but the
	; mapping, i.e. it checks for mirrors etc

	; test procedure:
	; test all 256 blocks of 32k
	; fill the last page of each block with a unique pattern per block
	; then go back to the start and check if patterns are still there

	; input params:
	; ac = start block (4k page)
	; xr = end block

m_upet_vmap
	.asc "UPET VRAM MAP",0

test_vmap .(

VMAPREG	=$e805
NUMMAPS	=8
TESTAREA=$8000
patt	=tmp1

	ldx #0
savel	lda TESTAREA,x
	sta buffer,x
	inx
	bne savel

	jsr testcode

	php
	tya
	pha
	ldx #0
restl	lda buffer,x
	sta TESTAREA,x
	inx
	bne restl

	pla
	plp
	bcs bad
	jmp okout
bad	;tya		; 32k bank
	jsr hexout
	jsr spout
	txa
	jsr hexout
	jsr spout
	ldy #22
	jmp errout	

testcode .(

	; -----------
	.(
	; start pattern
	lda #$55
	sta patt

	; fill
	ldy #0
fill	sty VMAPREG

	ldx #0
floop	lda patt
	sta TESTAREA,x
	inc patt
	inx
	bne floop
	inc patt	; change for next iteration
skip
	iny
	cpy #NUMMAPS	; number of video mappings
	bcc fill
	.)

	; -----------
	.(
	; start pattern
	lda #$55
	sta patt

	; check
	ldy #0
check	sty VMAPREG	; 32k bank

	; check 8296 mode as this maps in 4k steps not 2k
;	lda mode
;	and #MODE_8296
;	beq docheck
;	tya
;	lsr
;	bcc docheck
;	iny
;docheck
	tya
	; self-modifying code
	; calculate high byte address
	; 1=2k in, 2=4k in, etc
	; 1=$0800, 2=$1000, ...
	asl
	asl
	asl
	clc
	adc #>TESTAREA
	sta cmpl+2	; high address byte

	ldx #0
cloop	lda patt
	cmp TESTAREA,x
	bne err
cmpl	cmp TESTAREA+$080000,x
	bne err
	inc patt
	inx
	bne cloop
	inc patt
	iny
	cpy #NUMMAPS
	bne check
	.)

	clc
	.byt $24
err	sec
	lda #$02
	sta VMAPREG
	rts

	.)

	.)

