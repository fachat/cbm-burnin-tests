

test_up_rtc:
	; ----------------------------------
	; Micro-PET RTC 
	;
	; This test probes the RTC chip on the Micro-PET.
	; The test uses two bytes of the SRAM (which are
	; saved and restored), and the 1/100 counter to
	; check the clock.
	;
	.(

SPICTRL	=$e808
SPIDATA	=$e809
SPIPEEK	=$e80a

READ	=%00010011	; read RTCC/SRAM
WRITE	=%00010010	; write RTCC/SRAM

	; --------------------------
	; test SRAM

	.(
	lda #$20
	jsr r2reg
	stx backup
	sty backup+1

	lda pattrn
	tax
	eor #$ff
	tay
	lda #$20
	jsr w2reg

	lda #$20
	jsr r2reg
	cpx pattrn
	bne sramerr
	tya
	eor #$ff
	cmp pattrn
	bne sramerr

	ldx backup
	ldy backup+1
	lda #$20
	jsr w2reg

	jmp rtcc		; OK for SRAM

sramerr	lda #$00
	ldy #$16		; "BAD"
	jsr badout		; l7c8

	jsr spout
	lda #"R"
	jsr chrout
	jsr spout

	lda pattrn
	jsr hexout
	lda #$20
	jsr r2reg
	tya
	jsr hexout
	txa
	jsr hexout
	rts
	.)

	; --------------------------
	; test RTCC
rtcc	.(

	; init clock
	lda #1
	sta cntr2		; init wait timer

	ldy #0			; register 0+1
	jsr r1reg
	sta comp		; value to compare
	iny
	jsr r1reg
	tax
	bmi tloop		; is already running

	; enable battery backup
	ldy #4
	jsr r1reg
	ora #8
	jsr w1reg

	ldy #8
	jsr r1reg
	lda #64			; alm_no + sqwen + clko_01
	jsr w1reg

	ldy #1
	jsr r1reg
	ora #$80		; start clock
	jsr w1reg

	; wait for init
	lda #30
	sta cntr2

tloop	.(
	lda #0
	sta cntr		; count
	sta cntr+1		; count
tl0
	lda #0
	jsr r2reg

	inc cntr
	bne tl1
	inc cntr+1
	bne tl1
	lda cntr2
	beq badx

tl1 	cpy comp
	beq tl0
	sty comp

	dec cntr2
	bne tloop
	.)

	lda #0
	sta cntr
	sta cntr+1

tloop2	lda #0
	jsr r2reg
	cpy comp
	bne xloop
	inc cntr
	bne tloop2
	inc cntr+1
	bne tloop2

badx	pha
bad
	lda #$00
	ldy #$16
	jsr badout
	jsr spout
	lda #"T"
	jsr chrout
	lda comp
	jsr hexout
	pla
	jsr hexout
	lda cntr+1
	jsr hexout
	lda cntr
	jsr hexout
	rts

	; counter value depends on speed and code
	;
	; Note: as we cound changes in the 1/100s
	; timer value, we observe the variations that
	; the RTC chip does to keep the interval 
	; right on average, without having a 1/100
	; clock signal available.
	;
	; The timer runs 24 intervals with 41 internal
	; clock cycles, and one interval with 40 clock
	; cycles, i.e. 1024 internal clock cycles for 
	; 25/100 sec. The internal clock is 4096 kHz.
 
	; current values are
	; - $0209 = short 1/100 interval (one of 25)
	; - $0216/16 = normal 1/100 interval (24/25)	
	;
xloop	pha
	lda cntr+1
	cmp #1			; @ 12 Mhz
	bne bad
	lda cntr
	; lowest
	cmp #$ef		; @ 12 MHz
	bcc bad
	; highest
	cmp #$fd		; @ 12 MHz
	bcs bad
	; lower value of upper interval
	cmp #$fb
	bcs ok
	; upper value of lower interval
	cmp #$f0
	bcs bad
ok	
	pla
	jmp okout
	.)

	; ------------------------------------------------------------
	; access routines for the RTC chip. 
	; using the SPI routines
	;
	; note that the test program may run with 12MHz
	; and the SPI clock runs with 6 MHz to accomodate the 
	; maximum SPI clock speed of the RTC chip
	; So, to transfer 8 bits at 6MHz, we need 1.3 usec,
	; which is 16 clock cycles, or 8 NOPs between access.

spiwait	.(
			; jsr = 5 cycl
	lda #4		; 2
wl	sec		; N * 2
	sbc #1		; N * 2
	bne wl		; N * 3 - 1
	rts		; 3
	.)

w2reg	.(
	pha
	lda #5
	sta SPICTRL
	lda #WRITE
	sta SPIDATA
	jsr spiwait
	pla
	sta SPIDATA		; address first byte (from AC)
	jsr spiwait
	sty SPIDATA		; write first data byte 
	jsr spiwait
	stx SPIDATA		; write second data byte
	jsr spiwait
	lda #0
	sta SPICTRL		; deselect
	rts
	.)


	; YR= reg#, AC= data
w1reg	.(
	pha
	lda #5
	sta SPICTRL
	lda #WRITE
	sta SPIDATA
	jsr spiwait
	sty SPIDATA		; address first byte (from AC)
	jsr spiwait
	pla
	sta SPIDATA		; write first data byte 
	jsr spiwait
	lda #0
	sta SPICTRL		; deselect
	rts
	.)

r2reg	.(
	pha
	lda #5
	sta SPICTRL
	lda #READ
	sta SPIDATA
	jsr spiwait
	pla
	sta SPIDATA		; address first byte
	jsr spiwait
	lda #0
	sta SPIDATA		; trigger first data byte transfer
	jsr spiwait
	ldy SPIDATA		; read data and trigger next transfer
	jsr spiwait
	ldx SPIPEEK		; read second (last) byte
	sta SPICTRL		; deselect
	rts
	.)

	; YR= reg#, out -> AC=data, YR unmodified
r1reg	.(
	lda #5
	sta SPICTRL
	lda #READ
	sta SPIDATA
	jsr spiwait
	sty SPIDATA		; address first byte
	jsr spiwait
	lda #0
	sta SPIDATA		; trigger first data byte transfer
	jsr spiwait
	lda #0
	sta SPICTRL		; deselect
	lda SPIPEEK		; read last byte
	rts
	.)

backup	.byt 0,0
cntr	=backup
comp	.byt 0
cntr2	.byt 0
	.)


