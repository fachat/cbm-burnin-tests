
strout	.(
	sta ptr2
	stx ptr2+1
	ldy #0
cloop	lda (ptr2),y
	beq end
	jsr chrout
	iny
	bne cloop
end	rts
	.)

iniscr	.(
	lda #$80
	sta screen+1
	lda #0
	sta screen
	rts
	.)

	; -----------------------------
	; go to position a=col, y=line
	; make that column the target for CR
goxy	.(
	pha
	sty tmp1
	ldx #8
	lda #0
	sta revfl
	sta lineptr+1
gloop	asl
	rol lineptr+1
	asl tmp1
	bcc no
	clc
	adc cols
	bcc no
	inc lineptr+1
no	dex
	bne gloop
	clc
	adc screen
	sta lineptr
	lda lineptr+1
	adc screen+1
	sta lineptr+1
	pla
	clc
	adc lineptr
	sta lineptr
	sta scrptr
	lda #0
	adc lineptr+1
	sta lineptr+1
	sta scrptr+1
	rts
	.)

gox	.(
	clc
	adc lineptr
	sta scrptr
	lda #0
	sta revfl
	adc lineptr+1
	sta scrptr+1
	rts
	.)
	
	; -----------------------------
	; hexout
	; leaves x and y unmodified
hexout	.(
	pha
	lsr
	lsr
	lsr
	lsr
	jsr l169a	; l738
	pla
l169a	and #$0f
	cmp #$0a
	bcc l16a2
	adc #$06
l16a2	adc #$30
	.)
	; fall through

	.byt $2c
spout	lda #" "

	; -----------------------------
	; chrout
	; leaves x and y unmodified
chrout	.(
	pha			; save char
	sty tmp1		; save YR

	cmp #$93		; clear screen
	bne l16df

	; clear screen (8 mem pages starting from (screen)
	lda screen
	sta scrptr
	lda screen+1
	sta scrptr
	ldy #0
	lda #" "
clr1	sta (scrptr),y
	iny
	bne clr1
	inc scrptr+1
	lda scrptr+1
	sec
	sbc screen+1
	cmp #8
	bcc clr1

lhome	lda screen+1		; scr ptr to start of screen
	sta scrptr+1		; scr ptr
	sta lineptr+1		; start of line
	lda screen
	sta scrptr		; scr ptr
	sta lineptr		; start of line

l16db	ldy tmp1		; restore Y
	pla			; restore AC
	rts

	; other screen codes
l16df	cmp #$13
	beq lhome		; HOME

	cmp #$0d		; CR
	bne l16fc

	; carriage return
l16e7	clc
	lda lineptr		; start of line ptr
	adc cols
	sta lineptr
	sta scrptr		; to scr ptr
	lda #$00		; clr REV flag
	sta revfl
	adc lineptr+1
	sta lineptr+1
	sta scrptr+1		; scr ptr hi
	bne l16db

	
l16fc	and #$7f
	cmp #$12		; REV
	bne l170d

l1702	pla			; restore char
	pha
	and #$80
	eor #$80
	sta revfl		; store REV flag
	clv
	bvc l16db

l170d	and #$7f
	cmp #$20		; SPC
	bcc l16db		; lower, i.e. unprintable -> ignore

	; store char on screen
l1713	ldy #$00
	eor revfl		; REV flag
	sta (scrptr),y
	inc scrptr		; in scr ptr
	bne l16db
l171d	inc scrptr+1
	bne l16db
	.)


